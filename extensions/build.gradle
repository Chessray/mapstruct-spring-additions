apply plugin: "maven-publish"
apply plugin: "signing"

dependencies {
    implementation project(":annotations")
    implementation 'com.squareup:javapoet:1.13.0'
    implementation 'org.apache.commons:commons-lang3:3.11'

    testImplementation 'org.mapstruct:mapstruct:1.4.0.Final'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'commons-io:commons-io:2.8.0'
    testImplementation 'com.google.guava:guava:30.1-jre'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.7.7'
    testImplementation 'org.springframework:spring-core:5.0.0.RELEASE'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'mapstruct-spring-extensions'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'MapStruct Spring Extensions'
                description = 'Spring-specific MapStruct extensions'
                url = 'https://mapstruct.org/'
                packaging = 'jar'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Raimund Klein'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/mapstruct/mapstruct-spring-extensions.git'
                    developerConnection = 'scm:git:git://github.com/mapstruct/mapstruct-spring-extensions.git'
                    url = 'https://github.com/mapstruct/mapstruct-spring-extensions'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}