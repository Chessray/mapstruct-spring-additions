plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id "com.github.ben-manes.versions" version "0.39.0"
    id "org.springframework.boot" version "2.5.2" apply false
    id "io.spring.dependency-management" version "1.0.11.RELEASE" apply false
    id "org.asciidoctor.jvm.convert" version "3.3.2" apply false
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { otherIt -> version.toUpperCase().contains(otherIt) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

def specificMinimumVersions = { ModuleComponentIdentifier candidate ->
    candidate.group == "org.mapstruct" || candidate.group == "org.springframework"
}

dependencyUpdates {
    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                    reject('Release candidate')
                } else if (specificMinimumVersions(it.candidate)) {
                    reject('Minimal compatible version')
                }
            }
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "jacoco"

    group = 'org.mapstruct.extensions.spring'
    version = '0.1.0-SNAPSHOT'

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
        }
    }

    test {
        // Use junit platform for unit tests
        useJUnitPlatform()
    }

    jacoco {
        toolVersion = "0.8.5"
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = true
        }
    }

    check.dependsOn jacocoTestReport
}

wrapper {
    gradleVersion = '7.1.1'
}

